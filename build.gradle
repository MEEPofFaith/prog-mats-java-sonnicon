plugins{
    id "java"
    id "idea"
}

group pGroup

sourceCompatibility = 1.8
sourceSets.main{
    java.srcDir("src/")
    resources.srcDir("assets/")
}

repositories{
    mavenCentral()
    maven{url "https://jitpack.io"}
}

dependencies{
    implementation "com.github.younggam:multi-lib:$multilibVersion"
    
    compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"
    compileOnly "com.squareup:javapoet:$javapoetVersion"
    compileOnly "com.github.Anuken:jabel:$jabelVersion"
}

ext{
    isWindows = {
        return System.getProperty("os.name").toLowerCase(Locale.ROOT).contains("windows")
    }
}

jar{
    archiveFileName.set "${pArtifactFilename}Desktop.jar"

    from{
        configurations.runtimeClasspath.collect{
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

tasks.withType(JavaCompile){
    sourceCompatibility = 15
    targetCompatibility = 8

    options.encoding("UTF-8")
    if(JavaVersion.current() != JavaVersion.VERSION_1_8){
        options.compilerArgs.addAll(['--release', '8', '--enable-preview'])
    }

    doFirst{
        options.compilerArgs = options.compilerArgs.findAll{it != '--enable-preview'}
    }
}

tasks.withType(JavaExec){
    jvmArgs += "--enable-preview"
}

task alphableed doFirst{
    exec{
        workingDir "$rootDir"

        println "Alpha-bleeding sprites..."
        if(isWindows()){
            commandLine("cmd", "/c", "alpha-bleeding-windows.exe", "--replace", "./assets/sprites/")
        }else{
            commandLine("./alpha-bleed", "./assets/sprites/")
        }
    }
}

task dexify(type: Jar){
    archiveName "${pArtifactFilename}Dexed.jar"

    final File  jarArtifact = new File(tasks.jar.archiveFile.get().asFile.parent, "${pArtifactFilename}Desktop.jar"),
                dexedArtifact = new File(tasks.dexify.getTemporaryDir(), "dexed.jar")

    doFirst{
        exec{
            workingDir dexedArtifact.parent
            def command = ["d8", "--min-api", pMinApi, "--output", dexedArtifact, jarArtifact]
            if(isWindows()){
                commandLine("cmd", "/c", *command)
            }else{
                commandLine(*command)
            }
        }
    }

    from(zipTree(jarArtifact), zipTree(dexedArtifact))
}

task bleedBuild dependsOn "alphableed", "build"

task buildDex dependsOn "build", "dexify"

task bleedBuildDex dependsOn "alphableed", "buildDex"